use graph sdmGraph
drop query FeatureCollection
CREATE query FeatureCollection(vertex<phone> phoneId, int durationLimit = 600, int numOfCallLimit = 10) for graph sdmGraph {
  TYPEDEF TUPLE <Edge e, int totalDuration, int numOfCall> CallInfo;
  SumAccum<int> @stableCount;
  GroupByAccum<vertex<phone> tid, ListAccum<CallInfo> callInfo> @NBInfo;
  OrAccum @isInTargetGroup = false;
  OrAccum @isInStableGroup = false;

  /* for the purpose of visualization */ 
  # List store in each target vertex to store its own stable in group neighbor
  ListAccum<vertex<phone>> @inGroupStableNB;
  # Global variable used to store all in group stable connections edges
  ListAccum<Edge> @@inGroupStableConnectionList;
  # Global variable used to store all edges connecting Seed and its outgoing targets
  ListAccum<Edge> @@targetGroupEdgeList;

  Seed = {phoneId};
  //collect its target group
  TargetGroup = SELECT tgt
                FROM Seed:src - ((phone_phone|phone_phone_reversed):e) -> phone: tgt
                ACCUM
                  CASE WHEN e.type == "phone_phone" 
                         THEN @@targetGroupEdgeList += e, //only for visualization purpose
                              tgt.@isInTargetGroup = true
                  END,
                  //if the connection is stable, add one to its target @stableCount
                  IF e.total_duration > durationLimit AND e.num_of_call > numOfCallLimit THEN
                    tgt.@stableCount += 1
                  END
                POST-ACCUM
                  //if the target has @stableCount == 2 it is a stable connected target
                  IF tgt.@stableCount == 2 THEN
                     tgt.@isInStableGroup = true
                  END
                HAVING tgt.@isInTargetGroup == true
                ;

  //connections among the target group
  X = SELECT src
      FROM TargetGroup:src - ((phone_phone|phone_phone_reversed):e) -> phone: tgt
      WHERE tgt.@isInTargetGroup == true
      ACCUM 
        //update the neighbor info
        src.@NBInfo += (tgt -> CallInfo(e, e.total_duration, e.num_of_call))
      POST-ACCUM
        FOREACH (tid, infolist) in src.@NBInfo DO
          //if ListAccum<CallInfo> has two element then it has both call and callback from this target
          int stableCount = 0,
          IF infolist.size() == 2 THEN
            FOREACH info in infolist DO
              IF info.totalDuration > durationLimit AND info.numOfCall > numOfCallLimit THEN
                 stableCount = stableCount + 1
              END
            END
          END,
          //if it is stable connected in group target, increase its count
          IF stableCount == 2 THEN
            src.@inGroupStableNB += tid, //for visualization purpose
            FOREACH info in infolist DO
              @@inGroupStableConnectionList += info.e //for visualization purpose
            END
          END
        END
      ;
  PRINT Seed;
  PRINT TargetGroup;
  PRINT @@targetGroupEdgeList;
  PRINT @@inGroupStableConnectionList;
}
#install query collectFeaturesR
